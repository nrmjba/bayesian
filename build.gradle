ext {
    githubProjectName = 'bayesian'
    dataDir = "$rootDir/data"

    versions = [
            jersey: '1.12',
            javasimon: '3.3.0',
            slf4j: '1.7.1'
    ]
}

buildscript {
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

apply from: file('gradle/data-prep.gradle')
apply from: file('gradle/convention.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')

subprojects {
    group = "org.lenition.$githubProjectName"
}

project(':client') { }

project(':server') {
    apply plugin: 'gae'

    dependencies {
        gaeSdk 'com.google.appengine:appengine-java-sdk:1.8.2'

        compile "com.sun.jersey:jersey-server:$versions.jersey"
        compile "com.sun.jersey:jersey-servlet:$versions.jersey"
        compile "com.sun.jersey:jersey-json:$versions.jersey"
        compile "com.sun.jersey:jersey-grizzly2:$versions.jersey"
        compile "com.sun.jersey:jersey-core:$versions.jersey"
        compile 'com.google.code.gson:gson:2.2.4'

        providedCompile 'javax.servlet:servlet-api:2.5'

        testCompile 'junit:junit:4.11'
    }

    gae {
        optimizeWar = true     // see https://github.com/musketyr/gradle-fatjar-plugin/
        downloadSdk = true
        appcfg {
            email = 'seth.rylan@gmail.com'
            passIn = true

            logs {
                severity = 1
                outputFile = file('logs.txt')
            }

            app {
                id = 'bayesian'
            }
        }
    }
}
